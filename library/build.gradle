apply plugin: 'com.android.library'
apply plugin: 'robolectric'
apply plugin: 'maven'
apply plugin: 'findbugs'
apply plugin: 'pmd'

findbugs {
    toolVersion = "2.0.3"
}

dependencies {
    compile project(':third_party:gif_decoder')
    compile project(':third_party:gif_encoder')
    compile project(':third_party:disklrucache')
    compile 'com.android.support:support-v4:19.1.+'

    androidTestCompile 'org.hamcrest:hamcrest-core:1.3'
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile 'junit:junit:4.11'
    androidTestCompile 'org.mockito:mockito-all:1.9.5'
    androidTestCompile 'org.robolectric:robolectric:2.4-SNAPSHOT'
    // TODO: increase this to 2.0.+ when we compile against Java 7.
    androidTestCompile 'com.squareup.okhttp:mockwebserver:1.2.+'
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        applicationId 'com.bumptech.glide'
        minSdkVersion 10

        targetSdkVersion 19
        versionCode = VERSION_CODE
        versionName = VERSION_NAME
    }
}

afterEvaluate {
    task findbugs(type: FindBugs, dependsOn: assembleDebug) {

        description 'Run findbugs'
        group 'verification'

        classes = fileTree('build/intermediates/classes/debug/')
        source = fileTree('src/main/java')
        classpath = files(project.configurations.compile.asPath)

        effort = 'max'

        excludeFilter = file("findbugs-exclude.xml")

        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    findbugsTestDebug {
        enabled = false
    }

    check.dependsOn('findbugs')

    task pmd(type: Pmd) {

        description 'Run pmd'
        group 'verification'

        // If ruleSets is not empty, it seems to contain some
        // defaults which override rules in the ruleset file...
        ruleSets = []
        ruleSetFiles = files('pmd-ruleset.xml')
        source = fileTree('src/main/java')

        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    pmdTestDebug {
        enabled = false
    }

    check.dependsOn('pmd')
}
